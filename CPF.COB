       IDENTIFICATION DIVISION.
       PROGRAM-ID. CPFVALID.
       AUTHOR. ERICK ALESSI.

      ******************************************************************
      * Author: Erick Alessi
      * Date: 11.02.2019 (DD.MM.AAAA)
      * Purpose: Testar se um CPF          / Test if a CPF
      *          (Codigo de Pessoa Física) / (Kind of Brazilian ID doc)
      *           e valido                 / is valid
      * Tectonics: cobc
      ******************************************************************
       ENVIRONMENT DIVISION.

       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *O CPF É FORMADO POR 11 DIGITOS, 9 IDENTIFICADORES E 2 VALIDADORES
      *THE BRAZILIAN ID IS COMPOSED BY 11 DIGITS, BEING THE LAST
      *TWO USED FOR VALIDATION
       01 CPF.
           05 WRK-DIGITO  PIC 9(09)  VALUE ZEROES.
           05 WRK-VERIFI  PIC 9(02)  VALUE ZEROES.

        01 INDICES.
          05 IND1      PIC 9(2)      VALUE ZEROES.
          05 IND2      PIC 9(2)      VALUE ZEROES.

        77 AUX         PIC 9(1)      VALUE ZEROES.

        77 ACUMULADOR  PIC 9(4)      VALUE ZEROES.
      *INDICADOR DE VALIDO 1 PARA VALIDO 0 PARA INVALIDO
      *INDICATOR 1 TO VALID, 0 TO NOT VALID.
        77 VALIDO      PIC 9(1)      VALUE 1.

       PROCEDURE DIVISION.

       0001-PRINCIPAL SECTION.

           PERFORM 0100-INICIALIZAR.
           PERFORM 0200-PROCESSAR.
           PERFORM 0300-FINALIZAR.

       0001-PRINCIPAL-FIM. EXIT.

       0100-INICIALIZAR SECTION.

           ACCEPT CPF.

           PERFORM 0110-VALIDAR-ENTRADA.

       0100-INICIALIZAR-FIM. EXIT.

       0110-VALIDAR-ENTRADA SECTION.
      *SE NAO FOR NUMERICO, NAO E VALIDO
      *IF IT IS NOT NUMERIC, IT'S NOT VALID
           IF CPF IS NOT NUMERIC
              MOVE 0 TO VALIDO
              PERFORM 0300-FINALIZAR
           END-IF.

       0110-VALIDAR-ENTRADA-FIM. EXIT.

       0200-PROCESSAR SECTION.

           PERFORM 0210-TESTE-DEZ-DIGITO.
           PERFORM 0220-TESTE-ONZE-DIGITO.
           PERFORM 0230-TESTE-IGUAL.

       0200-PROCESSAR-FIM. EXIT.

       0210-TESTE-DEZ-DIGITO SECTION.
      *PRIMEIRAMENTE MULTIPLICA-SE OS 9 PRIMEIROS DIGITOS PELA SEQUENCIA
      *DECRESCENTE DE NUMEROS DE 10 A 2 E SOMA-SE OS RESULTADOS
      *FIRST IT'S NEEDED TO MULTIPLY THE FIRST 9 DIGITS BY THE SEQUENCE
      *FROM 10 TO 2 AND THEN SUM THE RESULTS
           MOVE 1 TO IND1.
           PERFORM VARYING IND2 FROM 10 BY -1 UNTIL IND2 < 2
               MOVE CPF(IND1:1) TO AUX
               COMPUTE ACUMULADOR = ACUMULADOR + AUX * IND2
               ADD 1 TO IND1
           END-PERFORM.
      *DEPOIS MULTIPLICA A SOMA POR 10 E DIVIDE POR 11
      *THEN THE SUM SHOULD BE MULTIPLY BY 10 AND DIVIDE BY 11
           COMPUTE ACUMULADOR = ACUMULADOR * 10.
           DIVIDE ACUMULADOR BY 11 GIVING AUX REMAINDER AUX
      *SENDO O RESTO O VALOR PROCURADO, E DEVE SER IGUAL AO 10 DIGITO
      *THE VALUE THAT MATTER IS THE REMAIN OF THIS DIVISION
      *AND IT SHOULD BE EQUALS TO THE 10TH CHARACTER
           IF AUX EQUAL CPF(10:1)
              MOVE 1 TO VALIDO
           ELSE
              MOVE 0 TO VALIDO
              PERFORM 0300-FINALIZAR
           END-IF.

       0210-TESTE-DEZ-DIGITO-FIM. EXIT.

       0220-TESTE-ONZE-DIGITO SECTION.
      *E IDENTICO AO TESTE DE 10 DIGITOS
      *IT'S IDENTICAL TO THE 10TH DIGIT TEST
           MOVE 1 TO IND1.
           MOVE 0 TO ACUMULADOR.
           PERFORM VARYING IND2 FROM 11 BY -1 UNTIL IND2 < 2
               MOVE CPF(IND1:1) TO AUX
               COMPUTE ACUMULADOR = ACUMULADOR + AUX * IND2
               ADD 1 TO IND1
           END-PERFORM.

           COMPUTE ACUMULADOR = ACUMULADOR * 10.
           DIVIDE ACUMULADOR BY 11 GIVING AUX REMAINDER AUX

           IF AUX EQUAL CPF(11:1)
               MOVE 1 TO VALIDO
           ELSE
               MOVE 0 TO VALIDO
           PERFORM 0300-FINALIZAR
           END-IF.

       0220-TESTE-ONZE-DIGITO-FIM. EXIT.
      *SE TODOS OS NUMEROS FOREM IGUAIS, NAO E UM CPF VALIDO, MAS PODE
      *PASSAR NOS TESTES ANTERIORES
      *IF EVERY NUMBER IS EQUAL, IT'S NOT VALID, BUT CAN PASS THROUGH
      *PREVIOUS TESTS
       0230-TESTE-IGUAL SECTION.

           PERFORM VARYING IND1 FROM 1 BY 1 UNTIL IND1 > 10
           COMPUTE IND2 = IND1 + 1
               IF CPF(IND1:1) EQUAL CPF(IND2:1)
                   MOVE 0 TO VALIDO
               ELSE
                   MOVE 1 TO VALIDO
               END-IF
           END-PERFORM.

       0230-TESTE-IGUAL-FIM. EXIT.

       0300-FINALIZAR SECTION.

           IF VALIDO EQUAL 1
               DISPLAY CPF ' VALIDO'
           ELSE
           IF VALIDO EQUAL 0
               DISPLAY CPF ' INVALIDO'
           END-IF.
           STOP RUN.

       0300-FINALIZAR-FIM. EXIT.
